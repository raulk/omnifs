image := "ghcr.io/raulk/omnifs:latest"
container := "omnifs"

check: build-providers
    cargo fmt --all --check
    cargo clippy -- -D warnings
    cargo test
    just check-providers

check-providers:
    cargo check -p omnifs-provider-github -p test-provider --target wasm32-wasip1
    cargo clippy -p omnifs-provider-github -p test-provider --target wasm32-wasip1 -- -D warnings
    cargo test -p omnifs-provider-github -p test-provider --target wasm32-wasip1 --no-run

build-providers:
    #!/usr/bin/env bash
    set -euo pipefail
    for manifest in providers/*/Cargo.toml; do
        grep -q '^\[package\]' "$manifest" || continue
        cargo component build --manifest-path "$manifest" --release --target-dir target
    done

test: build-providers
    cargo test --workspace

test-integration: build-providers
    cargo test -p omnifs-host --test runtime_test

build:
    docker build -t {{image}} .

start:
    #!/usr/bin/env bash
    set -euo pipefail
    export GITHUB_TOKEN="${GITHUB_TOKEN:-$(gh auth token)}"
    : "${SSH_AUTH_SOCK:?SSH_AUTH_SOCK must be set on the host}"
    docker rm -f {{container}} >/dev/null 2>&1 || true
    docker run -d \
      --name {{container}} \
      --device /dev/fuse \
      --cap-add SYS_ADMIN \
      --security-opt apparmor:unconfined \
      -e GITHUB_TOKEN="$GITHUB_TOKEN" \
      -e SSH_AUTH_SOCK=/ssh-agent \
      -e GIT_SSH_COMMAND='ssh -F /dev/null -o StrictHostKeyChecking=accept-new' \
      -v "$SSH_AUTH_SOCK:/ssh-agent" \
      -v "$(pwd)/scripts/demo.sh:/tmp/demo.sh:ro" \
      {{image}}
    for _ in $(seq 1 60); do
      if docker exec {{container}} sh -lc "grep -qs ' /github ' /proc/mounts"; then
        exit 0
      fi
      if ! docker ps --format '{{"{{.Names}}"}}' | grep -qx {{container}}; then
        docker exec {{container}} sh -lc 'cat /tmp/omnifs.log' >&2 || true
        exit 1
      fi
      sleep 1
    done
    docker exec {{container}} sh -lc 'cat /tmp/omnifs.log' >&2 || true
    exit 1

shell:
    docker exec -it {{container}} /bin/zsh

logs:
    docker exec -it {{container}} sh -lc 'cat /tmp/omnifs.log'

stop:
    docker rm -f {{container}}
